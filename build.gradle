plugins {
    id 'groovy'
    id 'scala'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.2'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.scala-lang:scala-library:2.11.7'
    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.1'
    testCompile 'org.scala-lang:scala-library:2.11.7'
    testCompile 'org.scalatest:scalatest_2.11:2.2.5'
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    from "${buildDir}/docs/groovydoc"
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

compileGroovy.dependsOn compileScala
compileTestGroovy.dependsOn compileTestScala

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'net.numa08'
            artifactId 'genrelease'
            version System.getenv('TRAVIS_TAG#v') ?: project.version
        }
    }
}

bintray {
    final String credential = project.hasProperty('bintray.credential') ?
                              project.property('bintray.credential') : System.getenv('BINTRAY') ?: ''
    user = credential.split(':').first()
    key  = credential.split(':').last()
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project.property('gradle.plugin.id')
        version {
            attributes = ['gradle-plugin': "${project.property('gradle.plugin.id')}:${project.group}:${project.name}"]
        }
    }
}

tasks.bintrayUpload.doFirst {
    assert !project.version.contains('SNAPSHOT')
}

task install(dependsOn: 'publishMavenPublicationToMavenLocal') {
}

apply from: file('gradle/plugin-generator.gradle')

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}
